'*******************************************************************************
'** 	Created By Peter Horsbøll Møller, Precisely
'** 	Program:
'** 	Modul:
'**
'*******************************************************************************

'-------------------------------------
Include "MapBasic.def"
Include "Icons.def"
Include "Menu.def"
'**default constants...
Include "Library\Defaults.def"

'-------------------------------------
Include "Library\DEBUGLib.def"
Include "Library\ERRORLib.def"
Include "Library\OBJLib.def"
Include "Library\TABLELib.def"

Include "DLGOffsetXYZ.def"

Declare Sub DLGOXYZDialog_OnLoad
Declare Sub DLGOXYZDialog_OKClicked
Declare Sub DLGOXYZCreateCopies_Clicked

Define	CTRL_TXT_OFFSET_X			100
Define	CTRL_TXT_OFFSET_Y			110
Define	CTRL_TXT_OFFSET_Z			120
Define	CTRL_LBL_OFFSET_Z			121
Define	CTRL_CHK_CREATE_COPIES		200
Define	CTRL_TXT_OFFSET_TIMES		300
Define	CTRL_LBL_OFFSET_TIMES		310
Define	CTRL_LST_CALCULATION_METHOD	400

Dim	mfOffsetX, mfOffsetY, mfOffsetZ As Float,
	mnOffsetTimes, mnCalculationMethod As Integer,
	mbCreateCopies As Logical,
	msBaseTab As String
'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOffsetXYZ(	  ByVal sUnits As String
					) As Logical

OnError GoTo ErrorOccured

DLGOffsetXYZ = FALSE

	If sUnits = "" Then
		sUnits = "m"
	End If
	msBaseTab	= SelectionInfo(SEL_INFO_TABLENAME)

	Dialog
		Title "Offset XYZ"
		Calling DLGOXYZDialog_OnLoad

		Control StaticText	Position 5,5
			Title "Offset " & SelectionInfo(SEL_INFO_NROWS) & " object(s) from '" & msBaseTab & "'"

		Control StaticText	Position 5,22
			Title "&X, " & sUnits & ":"
		Control EditText	Position 40,20		ID CTRL_TXT_OFFSET_X
			Width 70
'			Value FormatNumber$(Round(mfOffsetX, 0.0001))
			Value Format$(mfOffsetX, "0.0000")
		Control StaticText	Position 120,22
			Title "&Y, " & sUnits & ":"
		Control EditText	Position 155,20	ID CTRL_TXT_OFFSET_Y
			Width 70
			Value Format$(mfOffsetY, "0.0000")
		Control StaticText	Position 5,37		ID CTRL_LBL_OFFSET_Z
			Title "&Z:"
		Control EditText	Position 40,35		ID CTRL_TXT_OFFSET_Z
			Width 70
			Value Format$(mfOffsetZ, "0.0000")

		Control StaticText	Position 5,60
			Title "Calculation Method:"
		Control RadioGroup	Position 40,72		ID CTRL_LST_CALCULATION_METHOD
			Title "Cartesian;Spherical"
			Value DLGOXYZCalculationMethod()

		Control CheckBox	Position 110,60		ID CTRL_CHK_CREATE_COPIES
			Title "Create Copies of the Records"
			Value DLGOXYZCreateCopies()
			Calling DLGOXYZCreateCopies_Clicked
'			Disable Hide
		Control StaticText	Position 120,77		ID CTRL_LBL_OFFSET_TIMES
			Title "Offset Times:"
'			Disable Hide
		Control EditText	Position 175,75		ID CTRL_TXT_OFFSET_TIMES
			Value FormatNumber$(DLGOXYZOffsetTimes())
			Width 50
'			Disable Hide

		Control OKButton
			Calling DLGOXYZDialog_OKClicked
		Control CancelButton

	If not CommandInfo(CMD_INFO_DLG_OK) Then
		Exit Function
	End If

DLGOffsetXYZ = TRUE

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOffsetXYZ")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGOXYZDialog_OnLoad

OnError GoTo ErrorOccured

	Alter Control CTRL_LBL_OFFSET_Z Disable
	Alter Control CTRL_TXT_OFFSET_Z Disable
	If SystemInfo(SYS_INFO_MIVERSION) >= 2300 Then
		If TableInfo(msBaseTab, TAB_INFO_SUPPORT_MZ) Then
			Alter Control CTRL_LBL_OFFSET_Z Enable
			Alter Control CTRL_TXT_OFFSET_Z Enable
			Alter Control CTRL_TXT_OFFSET_Z Value "0"
		End If
	End If

	Call DLGOXYZCreateCopies_Clicked

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZDialog_OnLoad")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGOXYZDialog_OKClicked

OnError GoTo ErrorOccured

	mfOffsetX	= DeformatNumber$(ReadControlValue(CTRL_TXT_OFFSET_X))
	mfOffsetY	= DeformatNumber$(ReadControlValue(CTRL_TXT_OFFSET_Y))
	mfOffsetZ	= DeformatNumber$(ReadControlValue(CTRL_TXT_OFFSET_Z))

	If mfOffsetX = 0 And mfOffsetY = 0 And mfOffsetZ = 0 Then
		Note "Please specify an offset value for one of the dimensions!"
		Dialog Preserve
		Alter Control CTRL_TXT_OFFSET_X Active
		Exit Sub
	End If

	mbCreateCopies	= ReadControlValue(CTRL_CHK_CREATE_COPIES)
	If mbCreateCopies Then
		mnOffsetTimes	= DeformatNumber$(ReadControlValue(CTRL_TXT_OFFSET_TIMES))
		If mnOffsetTimes < 1 Then
			Note "Please specify a positive value for number of offset times!"
			Dialog Preserve
			Alter Control CTRL_TXT_OFFSET_TIMES Active
			Exit Sub
		End If
	End If

	mnCalculationMethod	= ReadControlValue(CTRL_LST_CALCULATION_METHOD)

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZDialog_OKClicked")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGOXYZCreateCopies_Clicked

OnError GoTo ErrorOccured

	If ReadControlValue(CTRL_CHK_CREATE_COPIES) Then
		Alter Control CTRL_LBL_OFFSET_TIMES	Enable
		Alter Control CTRL_TXT_OFFSET_TIMES	Enable
	Else
		Alter Control CTRL_LBL_OFFSET_TIMES	Disable
		Alter Control CTRL_TXT_OFFSET_TIMES	Disable
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZCreateCopies_Clicked")
	Call ERRShow()

End Sub



'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOXYZOffsetDistance( ByVal sXYZ As String) As Float

OnError GoTo ErrorOccured

	DLGOXYZOffsetDistance = 0

	Do Case sXYZ
		Case "X"
			DLGOXYZOffsetDistance = mfOffsetX
		Case "Y"
			DLGOXYZOffsetDistance = mfOffsetY
		Case "Z"
			DLGOXYZOffsetDistance = mfOffsetZ
	End Case

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZOffsetDistance")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGOXYZSetOffsetDistance(	  ByVal sXYZ As String
						, ByVal fDistance As Float)

OnError GoTo ErrorOccured

	Do Case sXYZ
		Case "X"
			mfOffsetX	= fDistance
		Case "Y"
			mfOffsetY	= fDistance
		Case "Z"
			mfOffsetZ	= fDistance
	End Case

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZSetOffsetDistance")
	Call ERRShow()

End Sub



'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOXYZOffsetTimes() As Integer

OnError GoTo ErrorOccured

	DLGOXYZOffsetTimes = IIf(mnOffsetTimes = 0, 1, mnOffsetTimes)

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZOffsetTimes")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOXYZCreateCopies() As Logical

OnError GoTo ErrorOccured

	DLGOXYZCreateCopies = mbCreateCopies
	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZCreateCopies")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOXYZCalculationMethod() As Integer

OnError GoTo ErrorOccured

	DLGOXYZCalculationMethod = IIf(mnCalculationMethod = 0, CALC_METHOD_CARTESIAN, mnCalculationMethod)

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZCalculationMethod")
	Call ERRShow()

End Function
'**********************************************************************************************''
Sub DLGOXYZSetCalculationMethod(ByVal nMethod As Integer)	'Use CALC_METHOD_CARTESIAN or CALC_METHOD_SPHERICAL from OBJLib.def

OnError GoTo ErrorOccured

	If nMethod In (1,2) Then
		mnCalculationMethod = nMethod
	End If
	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZSetCalculationMethod")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGOXYZCalculationMethodInitiated() As Logical

OnError GoTo ErrorOccured

	DLGOXYZCalculationMethodInitiated = (mnCalculationMethod <> 0)

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGOXYZCalculationMethodInitiated")
	Call ERRShow()

End Function