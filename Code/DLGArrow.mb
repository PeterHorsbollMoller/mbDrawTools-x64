'*******************************************************************************
'** 	Created By Peter Horsbøll Møller, Precisely
'** 	Program:
'** 	Modul:
'**
'*******************************************************************************

'-------------------------------------
Include "MapBasic.def"
Include "Icons.def"
Include "Menu.def"
'**default constants...
Include "Library\Defaults.def"

'-------------------------------------
Include "Library\DEBUGLib.def"
Include "Library\ERRORLib.def"

Include "DLGArrow.def"

Define CTRL_TXT_BUFFER_WIDTH			100
Define CTRL_TXT_BUFFER_RESOLUTION		110
Define CTRL_LST_ARROW_HEAD_TYPE		200
Define CTRL_TXT_ARROW_HEAD_WIDTH		210
Define CTRL_TXT_ARROW_HEAD_LENGTH		220
Define CTRL_LST_ARROW_TAIL_TYPE		300
Define CTRL_TXT_ARROW_TAIL_WIDTH		310
Define CTRL_TXT_ARROW_TAIL_LENGTH		320

Declare Sub DLGA_Load
Declare Sub DLGA_Unload
Declare Sub DLGA_ArrowHeadType_Clicked
Declare Sub DLGA_ArrowTailType_Clicked

Dim	mfArrowBufferWidth As Float,
	mnArrowBufferResolution As Integer,
	mnArrowType(2), mnArrowWidthPct(2), mnArrowLengthPct(2) As Integer

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGArrow() As Logical

OnError GoTo ErrorOccured

DLGArrow = FALSE

	Dialog
		Title "Arrow"
		Calling DLGA_Load

		Control GroupBox	Position 5,5
			Width 180		Height 40
			Title "Buffer"
		Control StaticText	Position 15,15
			Title "Width, m"
		Control EditText	Position 130,13	ID CTRL_TXT_BUFFER_WIDTH
			Width 50
			Value FormatNumber$(DLGAGetBufferWidth())
		Control StaticText	Position 15,30
			Title "Resolution"
		Control EditText	Position 130,28	ID CTRL_TXT_BUFFER_RESOLUTION
			Width 50
			Value FormatNumber$(DLGAGetBufferResolution())

		Control GroupBox	Position 5,55
			Width 180		Height 85
			Title "Arrow Head - end of line"
		Control RadioGroup	Position 15,65		ID CTRL_LST_ARROW_HEAD_TYPE
			Title "Rounded;Flat;Arrow"
			Value DLGAGetArrowType(ARROW_END_HEAD)
			Calling DLGA_ArrowHeadType_Clicked
		Control StaticText	Position 25,110
			Title "Width, % of buffer width"
		Control EditText	Position 130,108	ID CTRL_TXT_ARROW_HEAD_WIDTH
			Width 50
			Value FormatNumber$(DLGAGetArrowWidthPct(ARROW_END_HEAD))
		Control StaticText	Position 25,125
			Title "Length, % of buffer width"
		Control EditText	Position 130,123	ID CTRL_TXT_ARROW_HEAD_LENGTH
			Width 50
			Value FormatNumber$(DLGAGetArrowLengthPct(ARROW_END_HEAD))

		Control GroupBox	Position 5,150
			Width 180		Height 85
			Title "Arrow Tail - start of line"
		Control RadioGroup	Position 15,160	ID CTRL_LST_ARROW_TAIL_TYPE
			Title "Rounded;Flat;Arrow"
			Value DLGAGetArrowType(ARROW_END_TAIL)
			Calling DLGA_ArrowTailType_Clicked
		Control StaticText	Position 25,205
			Title "Width, % of buffer width"
		Control EditText	Position 130,203	ID CTRL_TXT_ARROW_TAIL_WIDTH
			Width 50
			Value FormatNumber$(DLGAGetArrowWidthPct(ARROW_END_TAIL))
		Control StaticText	Position 25,220
			Title "Length, % of buffer width"
		Control EditText	Position 130,218	ID CTRL_TXT_ARROW_TAIL_LENGTH
			Width 50
			Value FormatNumber$(DLGAGetArrowLengthPct(ARROW_END_TAIL))

		Control OKButton
			Calling DLGA_Unload
		Control CancelButton

	If not CommandInfo(CMD_INFO_DLG_OK) Then
		Exit Function
	End If

	DLGArrow = TRUE

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGArrow")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGA_Load

OnError GoTo ErrorOccured

	Call DLGA_ArrowHeadType_Clicked
	Call DLGA_ArrowTailType_Clicked

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGA_Load")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGA_Unload

Dim	nValue As Integer,
	fValue As Float

OnError GoTo ErrorOccured

	fValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_BUFFER_WIDTH)))
	If fValue <= 0 Then
		Note "Please enter a buffer widht larger than 0 meters"
		Alter Control CTRL_TXT_BUFFER_WIDTH Active
		Dialog Preserve
		Exit Sub
	End If
	mfArrowBufferWidth = fValue

	nValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_BUFFER_RESOLUTION)))
	If nValue <= 4 Then
		Note "Please enter a resolution larger than 4"
		Alter Control CTRL_TXT_BUFFER_RESOLUTION Active
		Dialog Preserve
		Exit Sub
	End If
	mnArrowBufferResolution = nValue

	'**Arrow Head
	mnArrowType(ARROW_END_HEAD) = ReadControlValue(CTRL_LST_ARROW_HEAD_TYPE)
	'Print "DLG: Arrow Type Head: " & mnArrowType(ARROW_END_HEAD)

	If mnArrowType(ARROW_END_HEAD) = ARROW_END_TYPE_ARROW Then
		nValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_ARROW_HEAD_WIDTH)))
		If nValue <= 100 Then
			Note "Please enter a arrow head width larger than 100%"
			Alter Control CTRL_TXT_ARROW_HEAD_WIDTH Active
			Dialog Preserve
			Exit Sub
		End If
		mnArrowWidthPct(ARROW_END_HEAD) = nValue
		nValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_ARROW_HEAD_LENGTH)))
		If nValue <= 10 Then
			Note "Please enter a arrow head length larger than 10%"
			Alter Control CTRL_TXT_ARROW_HEAD_LENGTH Active
			Dialog Preserve
			Exit Sub
		End If
		mnArrowWidthPct(ARROW_END_HEAD) = nValue
	End If

	'**Arrow Tail
	mnArrowType(ARROW_END_TAIL) = ReadControlValue(CTRL_LST_ARROW_TAIL_TYPE)
	'Print "DLG: Arrow Type Tail: " & mnArrowType(ARROW_END_TAIL)

	If mnArrowType(ARROW_END_TAIL) = ARROW_END_TYPE_ARROW Then
		nValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_ARROW_TAIL_WIDTH)))
		If nValue <= 100 Then
			Note "Please enter a arrow head tail larger than 100%"
			Alter Control CTRL_TXT_ARROW_TAIL_WIDTH Active
			Dialog Preserve
			Exit Sub
		End If
		mnArrowWidthPct(ARROW_END_TAIL) = nValue
		nValue	= Val(DeformatNumber$(ReadControlValue(CTRL_TXT_ARROW_TAIL_LENGTH)))
		If nValue <= 10 Then
			Note "Please enter a arrow tail length larger than 10%"
			Alter Control CTRL_TXT_ARROW_TAIL_LENGTH Active
			Dialog Preserve
			Exit Sub
		End If
		mnArrowWidthPct(ARROW_END_TAIL) = nValue
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGA_Unload")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGA_ArrowHeadType_Clicked

OnError GoTo ErrorOccured

	If ReadControlValue(CTRL_LST_ARROW_HEAD_TYPE) = ARROW_END_TYPE_ARROW Then
		Alter Control CTRL_TXT_ARROW_HEAD_WIDTH Enable
		Alter Control CTRL_TXT_ARROW_HEAD_LENGTH Enable
	Else
		Alter Control CTRL_TXT_ARROW_HEAD_WIDTH Disable
		Alter Control CTRL_TXT_ARROW_HEAD_LENGTH Disable
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGA_ArrowHeadType_Clicked")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'
'**********************************************************************************************''
Sub DLGA_ArrowTailType_Clicked

OnError GoTo ErrorOccured

	If ReadControlValue(CTRL_LST_ARROW_TAIL_TYPE) = ARROW_END_TYPE_ARROW Then
		Alter Control CTRL_TXT_ARROW_TAIL_WIDTH Enable
		Alter Control CTRL_TXT_ARROW_TAIL_LENGTH Enable
	Else
		Alter Control CTRL_TXT_ARROW_TAIL_WIDTH Disable
		Alter Control CTRL_TXT_ARROW_TAIL_LENGTH Disable
	End If

	Exit Sub
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGA_ArrowTailType_Clicked")
	Call ERRShow()

End Sub

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetBufferWidth() As Float

OnError GoTo ErrorOccured

	If mfArrowBufferWidth = 0 Then
		mfArrowBufferWidth = 100		'meters
	End If

	DLGAGetBufferWidth = mfArrowBufferWidth

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetBufferWidth")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetBufferResolution() As Integer

OnError GoTo ErrorOccured

	If mnArrowBufferResolution = 0 Then
		mnArrowBufferResolution = 30
	End If
	DLGAGetBufferResolution	= mnArrowBufferResolution

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetBufferResolution")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetArrowType(ByVal nArrowEnd As Integer) As Integer

OnError GoTo ErrorOccured

	Do Case nArrowEnd
		Case ARROW_END_HEAD
			If mnArrowType(nArrowEnd) = 0 Then
				mnArrowType(nArrowEnd) = ARROW_END_TYPE_ARROW
			End If
			DLGAGetArrowType = mnArrowType(nArrowEnd)

		Case ARROW_END_TAIL
			If mnArrowType(nArrowEnd) = 0 Then
				mnArrowType(nArrowEnd) = ARROW_END_TYPE_FLAT
			End If
			DLGAGetArrowType = mnArrowType(nArrowEnd)

	End Case

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetArrowType")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetArrowWidth(ByVal nArrowEnd As Integer) As Float

OnError GoTo ErrorOccured

	DLGAGetArrowWidth = (DLGAGetArrowWidthPct(nArrowEnd)/100) * DLGAGetBufferWidth()
	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetArrowWidth")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetArrowLength(ByVal nArrowEnd As Integer) As Float

OnError GoTo ErrorOccured

	DLGAGetArrowLength = (DLGAGetArrowLengthPct(nArrowEnd)/100) * DLGAGetBufferWidth()

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetArrowLength")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetArrowWidthPct(ByVal nArrowEnd As Integer) As Integer

OnError GoTo ErrorOccured

	Do Case nArrowEnd
		Case ARROW_END_HEAD
			If mnArrowWidthPct(nArrowEnd) = 0 Then
				mnArrowWidthPct(nArrowEnd)	= 250
			End If

			DLGAGetArrowWidthPct = mnArrowWidthPct(nArrowEnd)

		Case ARROW_END_TAIL
			If mnArrowWidthPct(nArrowEnd) = 0 Then
				mnArrowWidthPct(nArrowEnd)	= 250
			End If

			DLGAGetArrowWidthPct = mnArrowWidthPct(nArrowEnd)
	End Case

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetArrowWidthPct")
	Call ERRShow()

End Function

'**********************************************************************************************''
'Created by Peter Horsbøll Møller, Precisely
'Parameters:
'	:
'Return value:
'
'**********************************************************************************************''
Function DLGAGetArrowLengthPct(ByVal nArrowEnd As Integer) As Integer

OnError GoTo ErrorOccured

	Do Case nArrowEnd
		Case ARROW_END_HEAD
			If mnArrowLengthPct(nArrowEnd) = 0 Then
				mnArrowLengthPct(nArrowEnd)	= 250
			End If

			DLGAGetArrowLengthPct = mnArrowLengthPct(nArrowEnd)

		Case ARROW_END_TAIL
			If mnArrowLengthPct(nArrowEnd) = 0 Then
				mnArrowLengthPct(nArrowEnd)	= 250
			End If

			DLGAGetArrowLengthPct = mnArrowLengthPct(nArrowEnd)

	End Case

	Exit Function
'-------------------------
ErrorOccured:
	Call ERRCreate(Err(), Error$(), "DLGAGetArrowLengthPct")
	Call ERRShow()

End Function
